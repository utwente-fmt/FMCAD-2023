class Sensor {
    
    Main m;
    boolean init;
    
    int dist;
    
    ensures Perm(m, write)
            ** m == m0;
    ensures Perm(dist, write);
    ensures Perm(init, write)
            ** !init;
    Sensor(Main m0) {
        m = m0;
        init = false;
    }

    ensures \result >= 0 && \result < 256;
    int getDistance ();

    context Perm(m, 1\2)
            ** m != null
            ** Perm(m.sensor, read)
            ** m.sensor == this
            ** Perm(init, 1\2)
            ** !init;
    void run () {
    
        lock m;
        
        m.process_state = m.process_state[0 -> 0];
        m.event_state = m.event_state[0 -> 2];
        
        init = true;

        loop_invariant true
            ** Perm(m, 1\2)
            ** m != null
            ** held(m)
            ** m.global_invariant()
            ** m.sensor == this
            ** Perm(init, 1\2)
            ** init;
        while(true) {
            
            loop_invariant true
                ** Perm(m, 1\2)
                ** m != null
                ** held(m)
                ** m.global_invariant()
                ** m.sensor == this
                ** Perm(init, 1\2)
                ** init;
            while(m.process_state[0] != -1 || m.event_state[0] != -2){
                unlock m;
                lock m;
            }
            
            dist = getDistance();
                
            if (dist < m.MIN_DIST) {
                // od.notify(SC_ZERO_TIME)
                m.event_state = m.event_state[2 -> -1];
            }
            
            m.process_state = m.process_state[0 -> 0];
            m.event_state = m.event_state[0 -> 2];          
        }
       
        unlock m;
    }
}

   
class Controller {

    Main m;
    boolean init;
    
    boolean alarm_flag;

    ensures Perm(m, write)
            ** m == m0;
    ensures Perm(alarm_flag, write)
            ** !alarm_flag;
    ensures Perm(init, write)
            ** !init;
    Controller (Main m0) {
        m = m0;
        alarm_flag = false;
        init = false;
    }
    
    context Perm(m, 1\2)
            ** m != null
            ** Perm(m.controller, read)
            ** m.controller == this
            ** Perm(init, 1\2)
            ** !init;
    void run () {
        lock m;
        
        m.process_state = m.process_state[1->2];
        
        init = true;
        
        loop_invariant true
            ** Perm(m, 1\2)
            ** m != null
            ** held(m)
            ** m.global_invariant()
            ** m.controller == this
            ** Perm(init, 1\2)
            ** init;
        while (true) {

            loop_invariant true
                ** Perm(m, 1\2)
                ** m != null
                ** held(m)
                ** m.global_invariant()
                ** m.controller == this
                ** Perm(init, 1\2)
                ** init;
            while(m.process_state[1] != -1 || m.event_state[2] != -2){
                unlock m;
                lock m;
            }
            
            alarm_flag = true;
            
            assert m.sensor.dist < m.MIN_DIST;

            m.process_state = m.process_state[1->2];
            
        }

        unlock m;
    }
}

class Main {
    // Scheduling state
    seq<int> process_state;
    seq<int> event_state;
    
    // Object instances
    Sensor sensor;
    Controller controller;
    
    // Parameters
    int MIN_DIST;
    
    inline resource scheduler_permission_invariant() = true
  
        ** Perm(process_state, write) ** |process_state| == 2
        ** Perm(event_state, write) ** |event_state| == 3
    
        ** (\forall int i = 0 .. |process_state|;    {: process_state[i] :} == -1
                                                  || (   0 <= process_state[i]
                                                      && process_state[i] < |event_state|));
                                                      
    inline resource parameter_permission_invariant() = true
        ** Perm(MIN_DIST, read)
        ** MIN_DIST > 0;

    inline resource global_invariant() = true
        ** scheduler_permission_invariant()
        
        // PARAMETERS
        ** parameter_permission_invariant()

        // SENSOR
        ** Perm(sensor, read)
        ** sensor != null
        ** Perm(sensor.dist, write)
        ** Perm(sensor.init, 1\2)

        // CONTROLLER
        ** Perm(controller, read)
        ** controller != null
        ** Perm(controller.alarm_flag, write)
        ** Perm(controller.init, 1\2)
        
        // USER-DEFINED INVARIANTS
        
        // Helper invariants
        ** (process_state[0] == -1 || process_state[0] == 0)
        ** (process_state[1] == -1 || process_state[1] == 2)
        ** (event_state[0] != -1 && event_state[0] != 0)
        ** event_state[2] <= -1
        ** (!controller.init || (process_state[1] == -1 ==> process_state[0] != -1))
        ** (event_state[2] >= -1 ==> process_state[0] != -1)
        ** (event_state[2] >= -1 ==> sensor.dist < MIN_DIST)
        ** (controller.init && process_state[1] == -1 ==> sensor.dist < MIN_DIST)
        ;

    resource lock_invariant() = global_invariant();

    requires md0 > 0;
    ensures Perm(sensor, read)
            ** sensor != null
            ** Perm(sensor.m, 1\2)
            ** sensor.m == this
            ** Perm(sensor.init, 1\2)
            ** !sensor.init;
    ensures Perm(controller, read)
            ** controller != null
            ** Perm(controller.m, 1\2)
            ** controller.m == this
            ** Perm(controller.init, 1\2)
            ** !controller.init;
    ensures idle(sensor);
    ensures idle(controller);
    Main (int md0) {
        process_state = [-1,-1];
        event_state   = [-3,-3,-3];
        
        MIN_DIST = md0;

        sensor = new Sensor(this);
        controller = new Controller(this);
    }

    context Perm(sensor, read)
            ** sensor != null
            ** Perm(sensor.m, 1\2)
            ** sensor.m == this
            ** Perm(sensor.init, 1\2)
            ** !sensor.init;
    context Perm(controller, read)
            ** controller != null
            ** Perm(controller.m, 1\2)
            ** controller.m == this
            ** Perm(controller.init, 1\2)
            ** !controller.init;
    context idle(sensor);
    context idle(controller);
    void main() {
        lock this;
        fork sensor;
        fork controller;
        unlock this;
        
        // Scheduler
        loop_invariant true;
        while (true) {
            lock this;
                
            // Wake up processes waiting 0 time without delta delay
            immediate_wakeup();
            
            // Also reset any events that triggered
            reset_events_no_delta();

            // If still no processes are ready, advance delay or perform a delta cycle
            if (process_state[0] != -1 && process_state[1] != -1) {
                
                // Find the minimum delay until the next interesting point
                int min_advance = find_minimum_advance(event_state);
                if (min_advance == -1) {
                    min_advance = 0;
                }
                
                // Advance delay to the next interesting point
                event_state = [event_state[0] < -1 ? -3 : event_state[0] - min_advance,
                               event_state[1] < -1 ? -3 : event_state[1] - min_advance,
                               event_state[2] < -1 ? -3 : event_state[2] - min_advance];
                
                // Wake up processes if necessary
                wakeup_after_wait();
                
                // Also reset any events that triggered
                reset_all_events();
            }
            
            unlock this;
        }
        
        join sensor;
        join controller;
    }
    
    // =====================================================================
    // ==================== Scheduling Helper Functions ====================
    // =====================================================================
    
    // Context: The lock is held by the scheduler
    context held(this) ** scheduler_permission_invariant();
    // Everything that is not affected remains untouched
    ensures event_state == \old(event_state);
    // If the conditions are met, the entries of the sequences will be altered
    ensures    (    (   \old(process_state[0]) >= 0
                     && \old(event_state[\old(process_state[0])]) == 0)
                ==> (process_state[0] == -1))
            && (    (   \old(process_state[1]) >= 0
                     && \old(event_state[\old(process_state[1])]) == 0)
                ==> (process_state[1] == -1));
    // If the conditions are not met, the entries will remain the same
    ensures    (    (!(   \old(process_state[0]) >= 0
                       && \old(event_state[\old(process_state[0])]) == 0))
                ==> (process_state[0] == \old(process_state[0])))
            && (    (!(   \old(process_state[1]) >= 0
                       && \old(event_state[\old(process_state[1])]) == 0))
                ==> (process_state[1] == \old(process_state[1])));
    void immediate_wakeup();
    // ===============================================================
    
    // Context: The lock is held by the scheduler
    context held(this) ** scheduler_permission_invariant();
    // Everything that is not affected remains untouched
    ensures process_state == \old(process_state);
    // If the conditions are met, the entries of the sequences will be altered
    ensures    (    (\old(event_state[0]) == 0)
                ==> (event_state[0] == -2))
            && (    (\old(event_state[1]) == 0)
                ==> (event_state[1] == -2))
            && (    (\old(event_state[2]) == 0)
                ==> (event_state[2] == -2));
    // If the conditions are not met, the entries will remain the same
    ensures    (    (\old(event_state[0]) != 0)
                ==> (event_state[0] == \old(event_state[0])))
            && (    (\old(event_state[1]) != 0)
                ==> (event_state[1] == \old(event_state[1])))
            && (    (\old(event_state[2]) != 0)
                ==> (event_state[2] == \old(event_state[2])));
    void reset_events_no_delta();
    // ===============================================================
    
    // Requirements to check for this method to work
    requires |vals| == 3;
    // The result is a lower bound of all timeouts
    ensures    (vals[0] < -1 || \result <= vals[0])
            && (vals[1] < -1 || \result <= vals[1])
            && (vals[2] < -1 || \result <= vals[2]);
    // If no timeout exists, then the result is 0; otherwise, it is the minimum of all existing timeouts
    ensures    (    (   vals[0] < -1
                     && vals[1] < -1
                     && vals[2] < -1)
                ==> \result == 0)
            && (    (   vals[0] >= -1
                     || vals[1] >= -1
                     || vals[2] >= -1)
                ==> (   (vals[0] >= -1 && \result == vals[0])
                     || (vals[1] >= -1 && \result == vals[1])
                     || (vals[2] >= -1 && \result == vals[2])));
    pure int find_minimum_advance(seq<int> vals);
    // ===============================================================
    
    // Context: The lock is held by the scheduler
    context held(this) ** scheduler_permission_invariant();
    // Everything that is not affected remains untouched
    ensures event_state == \old(event_state);
    // If the conditions are met, the entries of the sequences will be altered
    ensures    (    (\old(process_state[0]) >= 0
                     && (   \old(event_state[\old(process_state[0])]) == 0
                         || \old(event_state[\old(process_state[0])]) == -1))
                ==> (process_state[0] == -1))
            && (    (\old(process_state[1]) >= 0
                     && (   \old(event_state[\old(process_state[1])]) == 0
                         || \old(event_state[\old(process_state[1])]) == -1))
                ==> (process_state[1] == -1));
    // If the conditions are not met, the entries will remain the same
    ensures    (    (!(   \old(process_state[0]) >= 0
                       && (   \old(event_state[\old(process_state[0])]) == 0
                           || \old(event_state[\old(process_state[0])]) == -1)))
                ==> (process_state[0] == \old(process_state[0])))
            && (    (!(   \old(process_state[1]) >= 0
                       && (   \old(event_state[\old(process_state[1])]) == 0
                           || \old(event_state[\old(process_state[1])]) == -1)))
                ==> (process_state[1] == \old(process_state[1])));
    void wakeup_after_wait();
    // ===============================================================
    
    // Context: The lock is held by the scheduler
    context held(this) ** scheduler_permission_invariant();
    // Everything that is not affected remains untouched
    ensures process_state == \old(process_state);
    // If the conditions are met, the entries of the sequences will be altered
    ensures    (    (   \old(event_state[0]) == 0
                     || \old(event_state[0]) == -1)
                ==> (event_state[0] == -2))
            && (    (   \old(event_state[1]) == 0
                     || \old(event_state[1]) == -1)
                ==> (event_state[1] == -2))
            && (    (   \old(event_state[2]) == 0
                     || \old(event_state[2]) == -1)
                ==> (event_state[2] == -2));
    // If the conditions are not met, the entries will remain the same
    ensures    (    (   \old(event_state[0]) != 0
                     && \old(event_state[0]) != -1)
                ==> (event_state[0] == \old(event_state[0])))
            && (    (   \old(event_state[1]) != 0
                     && \old(event_state[1]) != -1)
                ==> (event_state[1] == \old(event_state[1])))
            && (    (   \old(event_state[2]) != 0
                     && \old(event_state[2]) != -1)
                ==> (event_state[2] == \old(event_state[2])));
    void reset_all_events();
    
    // =====================================================================
    // =====================================================================
    // =====================================================================
}
